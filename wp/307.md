<!--
author: qingliangcn
date: 2012-06-16
title: Erlang R14以及之前版本的global模块的隐患
tags: Erlang,global
category: Erlang
status: publish
summary: 问题的引起：最近同事的一个项目遇到一个奇怪的问题，代码如下：G节点上运行类似如下代码：[erlang][begin    global:send(role_manager, {role_online, RoleID}),    gen_server:call({global, a
-->

<p class="MsoNormal"><span style="color: #000000;"><span style="font-family: 宋体; font-size: 9pt;">问题的引起：</span></span></p>
最近同事的一个项目遇到一个奇怪的问题，代码如下：

G节点上运行类似如下代码：

[erlang]
[begin
    global:send(role_manager, {role_online, RoleID}),
    gen_server:call({global, account_server}, {create_role, A}
end || _ &lt;- lists:seq(1, 1000)].
[/erlang]

role_manager, account_server都跑在W节点上

<!--more-->

role_manager收到{role_online, RoleID}消息的处理：[代码片段2]

[erlang]start_child(role_sup, {role_server, {role_server, start_link, []},transient, 30000, worker, [role_server]})[/erlang]

role_server的start_link: [代码片段3]

[erlang]
gen_server:start_link({global, role_XXXX}, role_server, [], []).[/erlang]

代码段1是很简单的一段代码，但是非常奇怪的就是这段代码居然用了N秒，注意是秒！

翻了源码没问题（蛋疼，我直接翻看的是R15B01的源码），后来跟同事聊天忽然想起来R15更新日志中优化了关于safe_whereis_name的代码，赶快去看看R14B02的源码：

两个文件gen.erl(gen_server:call最终会走到这里)和global.erl

[gen.erl]：

[erlang]%% Global by name
call({global, _Name}=Process, Label, Request, Timeout)
    when Timeout =:= infinity;
    is_integer(Timeout), Timeout &gt;= 0 -&gt;
    case where(Process) of
    Pid when is_pid(Pid) -&gt;
        Node = node(Pid),
        try do_call(Pid, Label, Request, Timeout)
        catch
            exit:{nodedown, Node} -&gt;
            %% A nodedown not yet detected by global,
            %% pretend that it was.
            exit(noproc)
        end;
    undefined -&gt;
        exit(noproc)
    end;[/erlang]

global名字首先where查询PID，发现用的是global:safe_whereis_name(Name)

[erlang]where({global, Name}) -&gt; global:safe_whereis_name(Name);
where({local, Name})  -&gt; whereis(Name).[/erlang]

跳转到[global.erl]:

[erlang]-spec safe_whereis_name(term()) -&gt; pid() | 'undefined'.
safe_whereis_name(Name) -&gt;
gen_server:call(global_name_server, {whereis, Name}, infinity).[/erlang]

首先是call，但是这个肯定不是性能低下的关键，再往下看：

[erlang]handle_call({whereis, Name}, From, S) -&gt;
    do_whereis(Name, From),
    {noreply, S};

do_whereis(Name, From) -&gt;
    case is_global_lock_set() of
    false -&gt;
        gen_server:reply(From, where(Name)); //注意这里
    true -&gt;
        send_again({whereis, Name, From})
end.[/erlang]

好吧，有锁，还有sleep：

[erlang]send_again(Msg) -&gt;
    Me = self(),
    spawn(fun() -&gt; timer(Me, Msg) end).
timer(Pid, Msg) -&gt;
    random_sleep(5),
    Pid ! Msg.[/erlang]

到了这里已经搞明白问题了，还在用R15以下版本的同学们赶快升级吧！

接下来顺便理理global lock的逻辑：

[erlang]is_global_lock_set() -&gt;
is_lock_set(?GLOBAL_RID).
is_lock_set(ResourceId) -&gt;
ets:member(global_locks, ResourceId).[/erlang]

看到了一个关键的表 global_locks，看看register_name的过程：

[erlang]-spec register_name(term(), pid()) -&gt; 'yes' | 'no'.
 register_name(Name, Pid) when is_pid(Pid) -&gt;
     register_name(Name, Pid, fun random_exit_name/3).

-type method() :: fun((term(), pid(), pid()) -&gt; pid() | 'none').

-spec register_name(term(), pid(), method()) -&gt; 'yes' | 'no'.
 register_name(Name, Pid, Method) when is_pid(Pid) -&gt;
     Fun = fun(Nodes) -&gt;
         case (where(Name) =:= undefined) andalso check_dupname(Name, Pid) of
             true -&gt;
                 gen_server:multi_call(Nodes,
                                       global_name_server,
                                       {register, Name, Pid, Method}),
                 yes;
             _ -&gt;
                 no
         end
     end,
     ?trace({register_name, self(), Name, Pid, Method}),
gen_server:call(global_name_server, {registrar, Fun}, infinity).

handle_call({registrar, Fun}, From, S) -&gt;
    S#state.the_registrar ! {trans_all_known, Fun, From},
 {noreply, S};[/erlang]

S#state.the_registrar是什么？ 看注释：

[erlang]%% The registrar is a helper process that registers and unregisters
%% names. Since it never dies it assures that names are registered and
%% unregistered on all known nodes. It is started by and linked to
%% global_name_server.
start_the_registrar() -&gt;
    spawn_link(fun() -&gt; loop_the_registrar() end).

loop_the_registrar() -&gt;
    receive
        {trans_all_known, Fun, From} -&gt;
            ?trace({loop_the_registrar, self(), Fun, From}),
            gen_server:reply(From, trans_all_known(Fun));
    Other -&gt;
            unexpected_message(Other, register)
    end,
loop_the_registrar().[/erlang]

围观trans_all_known(Fun)：

[erlang]trans_all_known(Fun) -&gt;
    Id = {?GLOBAL_RID, self()},
    Nodes = set_lock_known(Id, 0),
    try
        Fun(Nodes)
    after
        delete_global_lock(Id, Nodes)
    end.

set_lock_known(Id, Times) -&gt;
    Known = get_known(),
    Nodes = [node() | Known],
    Boss = the_boss(Nodes),
    %% Use the  same convention (a boss) as lock_nodes_safely. Optimization.
    case set_lock_on_nodes(Id, [Boss]) of
        true -&gt;
            case lock_on_known_nodes(Id, Known, Nodes) of
                true -&gt;
                    Nodes;
                false -&gt;
                    del_lock(Id, [Boss]),
                    random_sleep(Times),
                    set_lock_known(Id, Times+1)
            end;
        false -&gt;
            random_sleep(Times),
            set_lock_known(Id, Times+1)
    end.

lock_on_known_nodes(Id, Known, Nodes) -&gt;
    case set_lock_on_nodes(Id, Nodes) of
        true -&gt;
            (get_known() -- Known) =:= [];
        false -&gt;
            false
end.
set_lock_on_nodes(_Id, []) -&gt;
    true;
set_lock_on_nodes(Id, Nodes) -&gt;
    case local_lock_check(Id, Nodes) of
        true -&gt;
            Msg = {set_lock, Id},
            {Replies, _} =
                gen_server:multi_call(Nodes, global_name_server, Msg),
            ?trace({set_lock,{me,self()},Id,{nodes,Nodes},{replies,Replies}}),
            check_replies(Replies, Id, Replies);
        false=Reply -&gt;
            Reply
    end.

%% Probe lock on local node to see if one should go on trying other nodes.
local_lock_check(_Id, [_] = _Nodes) -&gt;
    true;
local_lock_check(Id, Nodes) -&gt;
    not lists:member(node(), Nodes) orelse (can_set_lock(Id) =/= false).
[/erlang]

中间通过boss方法获得一个裁决node，先在boss node上获得锁，成功则锁住其他节点，失败则timer sleep；很多同学看到sleep可能会下意识的觉得蛋疼，但是实际上这是erlang天生分布式的特性决定，由于sleep的存在才会有自动重试，上层才会通常不用关心分布式的细节。继续走:

[erlang]handle_set_lock(Id, Pid, S) -&gt;
    ?trace({handle_set_lock, Id, Pid}),
    case can_set_lock(Id) of
        {true, PidRefs} -&gt;
        case pid_is_locking(Pid, PidRefs) of
        true -&gt;
                    {true, S};
        false -&gt;
                    {true, insert_lock(Id, Pid, PidRefs, S)}
        end;
        false=Reply -&gt;
            {Reply, S}
end.

can_set_lock({ResourceId, LockRequesterId}) -&gt;
    case ets:lookup(global_locks, ResourceId) of
    [{ResourceId, LockRequesterId, PidRefs}] -&gt;
            {true, PidRefs};
    [{ResourceId, _LockRequesterId2, _PidRefs}] -&gt;
            false;
    [] -&gt;
            {true, []}
    end.

insert_lock({ResourceId, LockRequesterId}=Id, Pid, PidRefs, S) -&gt;
    {RPid, Ref} = do_monitor(Pid),
    true = ets:insert(global_pid_ids, {Pid, ResourceId}),
    true = ets:insert(global_pid_ids, {Ref, ResourceId}),
    Lock = {ResourceId, LockRequesterId, [{Pid,RPid,Ref} | PidRefs]},
    true = ets:insert(global_locks, Lock),
    trace_message(S, {ins_lock, node(Pid)}, [Id, Pid]).[/erlang]

我们看到了其实就是用protected ets来实现的一个内存锁。

总的来说global的实现是比较简单的，是erlang dist的基础物件，中间的一些细节也是非常不错、非常巧妙的，不过也看到了中间存在的巨大的损耗和隐患（call/sleep），因此global不要滥用，适度就好。